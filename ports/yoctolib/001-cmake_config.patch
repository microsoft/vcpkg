diff --git a/Sources/CMakeLists.txt b/Sources/CMakeLists.txt
index 678fff5..f60b212 100644
--- a/Sources/CMakeLists.txt
+++ b/Sources/CMakeLists.txt
@@ -181,10 +181,41 @@ add_library (YoctoLib ${YoctoLibCppSources} ${YoctoLibHSources})
 
 # add yapi low level api
 add_subdirectory(yapi)
-target_link_libraries(YoctoLib PRIVATE yapi)
-target_include_directories (YoctoLib PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
+target_link_libraries(YoctoLib PUBLIC yapi)
+target_include_directories (YoctoLib
+	PUBLIC
+		$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
+		$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
+)
 set_target_properties(YoctoLib PROPERTIES SOVERSION 1)
 set_target_properties(YoctoLib PROPERTIES LIBRARY_OUTPUT_NAME yocto)
-install(TARGETS YoctoLib DESTINATION ${CMAKE_INSTALL_LIBDIR})
 install(FILES ${YoctoLibHSources} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
 install(FILES yapi/yapi.h yapi/ydef.h yapi/yversion.h yapi/yjson.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/yapi)
+
+include(CMakePackageConfigHelpers)
+write_basic_package_version_file(
+	"${CMAKE_CURRENT_BINARY_DIR}/unofficial-yoctolib-version.cmake"
+	VERSION ${VERSION}
+	COMPATIBILITY SameMajorVersion
+)
+configure_package_config_file(
+	"${CMAKE_CURRENT_SOURCE_DIR}/cmake/unofficial-yoctolib-config.cmake.in"
+	"${CMAKE_CURRENT_BINARY_DIR}/unofficial-yoctolib-config.cmake"
+	INSTALL_DESTINATION share/unofficial-yoctolib
+)
+install(TARGETS YoctoLib yapi
+	EXPORT YoctoLibTargets
+	RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
+	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
+	ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
+)
+install(EXPORT YoctoLibTargets
+	FILE unofficial-yoctolib-targets.cmake
+	NAMESPACE unofficial::yoctolib::
+	DESTINATION share/unofficial-yoctolib
+)
+install(FILES
+	"${CMAKE_CURRENT_BINARY_DIR}/unofficial-yoctolib-config.cmake"
+	"${CMAKE_CURRENT_BINARY_DIR}/unofficial-yoctolib-version.cmake"
+	DESTINATION share/unofficial-yoctolib
+)
diff --git a/Sources/cmake/unofficial-yoctolib-config.cmake.in b/Sources/cmake/unofficial-yoctolib-config.cmake.in
new file mode 100644
index 0000000..6f442b2
--- /dev/null
+++ b/Sources/cmake/unofficial-yoctolib-config.cmake.in
@@ -0,0 +1,14 @@
+@PACKAGE_INIT@
+
+include(CMakeFindDependencyMacro)
+
+if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
+    set(THREADS_PREFER_PTHREAD_FLAG TRUE)
+    find_dependency(Threads)
+    find_dependency(PkgConfig)
+    pkg_check_modules(libusb REQUIRED IMPORTED_TARGET libusb-1.0)
+endif()
+
+include(${CMAKE_CURRENT_LIST_DIR}/unofficial-yoctolib-targets.cmake)
+
+check_required_components(unofficial-yoctolib)
\ No newline at end of file
diff --git a/Sources/yapi/CMakeLists.txt b/Sources/yapi/CMakeLists.txt
index f6b571f..ca02890 100644
--- a/Sources/yapi/CMakeLists.txt
+++ b/Sources/yapi/CMakeLists.txt
@@ -139,11 +139,18 @@ if(USE_YSSL)
 	target_include_directories(yapi PRIVATE mbedtls/include)
 endif()
 
-target_include_directories (yapi PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
+target_include_directories(yapi PUBLIC
+	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
+	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/yapi>
+)
 
 # add pthread usb-1.0 library only on linux
 if(${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
-target_link_libraries (yapi LINK_PUBLIC pthread usb-1.0)
+	set(THREADS_PREFER_PTHREAD_FLAG TRUE)
+	find_package(Threads REQUIRED)
+	find_package(PkgConfig REQUIRED)
+	pkg_check_modules(libusb REQUIRED IMPORTED_TARGET libusb-1.0)
+	target_link_libraries(yapi LINK_PUBLIC Threads::Threads PkgConfig::libusb)
 endif()
 
 if (APPLE)
@@ -159,7 +166,3 @@ if (APPLE)
 endif()
 
 set_target_properties(yapi PROPERTIES SOVERSION 1)
-install(TARGETS yapi DESTINATION ${CMAKE_INSTALL_LIBDIR})
-install(FILES yapi.h ydef.h yversion.h yjson.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/yapi)
-
-
diff --git a/Sources/yapi/yproto.h b/Sources/yapi/yproto.h
index 5fbadb3..ab0a801 100644
--- a/Sources/yapi/yproto.h
+++ b/Sources/yapi/yproto.h
@@ -163,7 +163,7 @@ typedef struct {
 /*****************************************************************************
   LINUX SPECIFIC HEADER
  ****************************************************************************/
-#include <libusb-1.0/libusb.h>
+#include <libusb.h>
 #endif
 
 /*****************************************************************************
