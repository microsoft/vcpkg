# Template for mcapConfig.cmake -- a simple config wrapper for the mcap library

include(CMakeFindDependencyMacro)
foreach(_DEPENDENCY IN ITEMS @MCAP_FIND_DEPENDENCIES@)
    find_dependency(${_DEPENDENCY})
endforeach()

# Protect against multiple inclusion
if (TARGET mcap::mcap)
    return ()
endif ()

# Compute the installation prefix relative to this file, which is located in ${_IMPORT_PREFIX}/share/mcap
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_DIR}/../.." ABSOLUTE)

# Create imported mcap::mcap target
add_library(mcap::mcap INTERFACE IMPORTED)
target_include_directories(mcap::mcap INTERFACE "${_IMPORT_PREFIX}/include")
if (NOT "@MCAP_LINK_LIBRARIES@" STREQUAL "")
    target_link_libraries(mcap::mcap INTERFACE @MCAP_LINK_LIBRARIES@)
endif ()
if (NOT "@MCAP_COMPILE_DEFINITIONS@" STREQUAL "")
    target_compile_definitions(mcap::mcap INTERFACE @MCAP_COMPILE_DEFINITIONS@)
endif ()
