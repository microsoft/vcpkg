include(CMakeFindDependencyMacro)

if(WIN32)
    message(WARNING "find_package(unofficial-apr) is deprecated.\nUse find_package(apr) instead")
    find_dependency(apr CONFIG)

    if(TARGET apr::apr-1 AND NOT TARGET unofficial::apr::apr-1)
        add_library(unofficial::apr::apr-1 INTERFACE IMPORTED)
        target_link_libraries(unofficial::apr::apr-1 INTERFACE apr::apr-1)
    elseif(TARGET apr::libapr-1 AND NOT TARGET unofficial::apr::libapr-1)
        add_library(unofficial::apr::libapr-1 INTERFACE IMPORTED)
        target_link_libraries(unofficial::apr::libapr-1 INTERFACE apr::libapr-1)
    endif()

    if(TARGET apr::aprapp-1 AND NOT TARGET unofficial::apr::aprapp-1)
        add_library(unofficial::apr::aprapp-1 INTERFACE IMPORTED)
        target_link_libraries(unofficial::apr::aprapp-1 INTERFACE apr::aprapp-1)
    elseif(TARGET apr::libaprapp-1 AND NOT TARGET unofficial::apr::libaprapp-1)
        add_library(unofficial::apr::libaprapp-1 INTERFACE IMPORTED)
        target_link_libraries(unofficial::apr::libaprapp-1 INTERFACE apr::libaprapp-1)
    endif()
else()
    find_dependency(Threads)
    
    find_library(APR_LIBRARY_RELEASE NAMES apr-1 PATHS "${CMAKE_CURRENT_LIST_DIR}/../../lib" NO_DEFAULT_PATH)
    find_library(APR_LIBRARY_DEBUG NAMES apr-1 PATHS "${CMAKE_CURRENT_LIST_DIR}/../../debug/lib" NO_DEFAULT_PATH)
    find_path(APR_INCLUDE_DIR apr.h PATH_SUFFIXES apr-1)
    
    include(SelectLibraryConfigurations)
    select_library_configurations(APR)
    
    if(NOT TARGET apr::apr-1)
        add_library(apr::apr-1 UNKNOWN IMPORTED)
        set_target_properties(apr::apr-1 PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${APR_INCLUDE_DIR}"
            INTERFACE_LINK_LIBRARIES "Threads::Threads"
        )
        if(APR_LIBRARY_RELEASE)
            set_property(TARGET apr::apr-1 APPEND PROPERTY IMPORTED_CONFIGURATIONS RELEASE)
            set_target_properties(apr::apr-1 PROPERTIES IMPORTED_LOCATION_RELEASE "${APR_LIBRARY_RELEASE}")
        endif()
        if(APR_LIBRARY_DEBUG)
            set_property(TARGET apr::apr-1 APPEND PROPERTY IMPORTED_CONFIGURATIONS DEBUG)
            set_target_properties(apr::apr-1 PROPERTIES IMPORTED_LOCATION_DEBUG "${APR_LIBRARY_DEBUG}")
        endif()
        if(NOT APR_LIBRARY_RELEASE AND NOT APR_LIBRARY_DEBUG)
            set_target_properties(apr::apr-1 PROPERTIES IMPORTED_LOCATION "${APR_LIBRARY}")
        endif()
    endif()
    
    if(NOT TARGET unofficial::apr::apr-1)
        add_library(unofficial::apr::apr-1 INTERFACE IMPORTED)
        target_link_libraries(unofficial::apr::apr-1 INTERFACE apr::apr-1)
    endif()
endif()
