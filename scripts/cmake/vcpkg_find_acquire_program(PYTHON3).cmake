if(CMAKE_HOST_WIN32)
    set(program_name python)
    set(program_version 3.12.6)
    if(DEFINED ENV{PROCESSOR_ARCHITEW6432})
        set(build_arch $ENV{PROCESSOR_ARCHITEW6432})
    else()
        set(build_arch $ENV{PROCESSOR_ARCHITECTURE})
    endif()
    if(build_arch MATCHES "^(ARM|arm)64$")
        set(tool_subdirectory "python-${program_version}-arm64")
        set(download_urls "https://www.python.org/ftp/python/${program_version}/python-${program_version}-embed-arm64.zip")
        set(download_filename "python-${program_version}-embed-arm64.zip")
        set(download_sha512 AE87B0FDF9FA75C68A9ECF64628474819D19A57B79FB2D9BB1C3265833EE57CCC1C85C4CF696F90B80C7FAA53ADFD35E893886365202A90EEE0B879B29DA9CA1)
    elseif(build_arch MATCHES "(amd|AMD)64")
        set(tool_subdirectory "python-${program_version}-x64")
        set(download_urls "https://www.python.org/ftp/python/${program_version}/python-${program_version}-embed-amd64.zip")
        set(download_filename "python-${program_version}-embed-amd64.zip")
        set(download_sha512 43E9EAB6FCE7E62F1BADF478706E3DF7F65A9BB0FDFF2EE01F71C65425ACD8D73E4E0DCC64A6EE59A7D934BE7271A4F0B9FE3E0DA62A5BA636663F628BD7FBD7)
    else()
        set(tool_subdirectory "python-${program_version}-x86")
        set(download_urls "https://www.python.org/ftp/python/${program_version}/python-${program_version}-embed-win32.zip")
        set(download_filename "python-${program_version}-embed-win32.zip")
        set(download_sha512 1E1BAE458232E336799DDA220366D6BF9F4F54E4FE7E341E287FF9DB773E8D7C3E03004BA820C7DDB04EF1CE93064F002B96CA4E49326D749055E7D5F3C9ED2D)
    endif()

    set(paths_to_search "${DOWNLOADS}/tools/python/${tool_subdirectory}")

    # We want to be able to import stuff from outside of this embeddable package.
    # https://docs.python.org/3/library/sys_path_init.html#pth-files
    string(REGEX MATCH "^3\\.[0-9]+" _python_version_plain "${program_version}")
    string(REPLACE "." "" _python_version_plain "${_python_version_plain}")
    vcpkg_list(SET post_install_command "${CMAKE_COMMAND}" -E rm "python${_python_version_plain}._pth")
else()
    set(program_name python3)
    set(brew_package_name "python")
    set(apt_package_name "python3")
endif()
